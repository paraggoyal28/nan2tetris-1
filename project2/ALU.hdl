// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    //// Replace this comment with your code.
        Mux16(a = x, b = false, sel = zx, out = temp1);
        Not16(in = temp1, out = nottemp1);
        Mux16(a = temp1, b= nottemp1, sel= nx, out= temp2);

        Mux16(a = y, b = false, sel = zy, out = temp3);
        Not16(in = temp3, out = nottemp3);
        Mux16(a = temp3, b= nottemp3, sel= ny, out= temp4);

        Add16(a = temp2, b = temp4, out = tempAdd);
        And16(a = temp2, b = temp4, out = tempAnd);
        Mux16(a = tempAnd, b = tempAdd, sel=f, out=temp5);

        Not16(in = temp5, out=nottemp5);
        Mux16(a = temp5, b = nottemp5, sel= no, out=out, out[0..7]=preout2, out[8..15] = preout3, out[15] = ng);
        
        Or8Way(in = preout2, out=orOfPreout2);
        Or8Way(in = preout3, out=orOfPreout3);
        Or(a = orOfPreout2, b = orOfPreout3, out = orOfOut);
        Not(in = orOfOut, out=zr); 

}


